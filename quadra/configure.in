dnl Process this file with autoconf to produce a configure script.
AC_REVISION($Id$)

AC_INIT(source/quadra.cpp)

AC_PATH_XTRA
AC_ARG_WITH(svgalib,
  [  --without-svgalib       Svgalib support])
AC_ARG_ENABLE(debug,
  [  --enable-debug          debugging options])
AC_ARG_ENABLE(profile,
  [  --enable-profile        profiling options])

dnl Checks for programs.
AC_PROG_CXX
AC_PROG_INSTALL

CPPFLAGS="$CPPFLAGS$X_CFLAGS"
LDFLAGS="$LDFLAGS$X_LIBS"

dnl Checks for libraries.
AC_CHECK_LIB(z, compress,, nozlib=1)
AC_CHECK_LIB(dl, dlopen,, nodl=1)
AC_CHECK_LIB(png, png_create_read_struct,, nolibpng=1)
AC_CHECK_LIB(vga, vga_init,, nosvgalib=1)
AC_CHECK_LIB(vgagl, gl_setcontext,, nosvgalib=1, -lvga)
AC_CHECK_LIB(X11, XOpenDisplay,, have_x=no)
AC_CHECK_LIB(Xext, XShmAttach,, have_x=no)
AC_CHECK_LIB(Xxf86vm, XF86VidModeSwitchToMode,, have_x=no)
AC_CHECK_LIB(Xpm, XpmCreatePixmapFromData,, have_x=no)

dnl Checks for header files.
AC_CHECK_HEADERS(vga.h vgagl.h vgamouse.h vgakeyboard.h,, nosvgalib=1)
AC_CHECK_HEADERS(X11/Xlib.h X11/xpm.h,, have_x=no)
AC_CHECK_HEADERS(X11/extensions/XShm.h X11/extensions/xf86vmode.h,,
	have_x=no, [
#include <X11/Xlib.h>
])
AC_CHECK_HEADER(zlib.h,, nozlib=1)
AC_CHECK_HEADER(png.h,, nolibpng=1)
AC_CHECK_HEADERS(machine/endian.h)
AC_CHECK_HEADERS(machine/byte_order.h)
AC_CHECK_HEADERS(linux/cdrom.h)
AC_CHECK_HEADERS(linux/soundcard.h)
AC_CHECK_HEADERS(mcheck.h)

dnl Checks for typedefs, structures, and compiler characteristics.

dnl Checks for library functions.
AC_CHECK_FUNCS(basename)

if test "$with_svgalib" = "yes" -a -n "$nosvgalib"; then
	AC_MSG_ERROR(Svgalib could not be found.)
fi

if test -z "$with_svgalib" -a -z "$nosvgalib"; then
	with_svgalib=yes
else
	with_svgalib=no
fi

if test -z "$with_x" -a "$have_x" = "yes"; then
	with_x=yes
else
	with_x=no
fi

if test -n "$nodl"; then
	AC_MSG_WARN([dl is required and could not be found!])
	missing_required=yes
fi

if test -n "$nozlib"; then
	AC_MSG_WARN([zlib is required and could not be found!])
	missing_required=yes
fi

if test -n "$nolibpng"; then
	AC_MSG_WARN([libpng is required and could not be found!])
	missing_required=yes
fi

if test "$with_x" = "no"; then
	AC_MSG_WARN([X11 is required and could not be found!])
	missing_required=yes
fi

if test -n "$missing_required"; then
	AC_MSG_ERROR([Required dependencies missing.])
fi

AC_SUBST(with_x)
AC_SUBST(with_svgalib)
AC_SUBST(enable_debug)
AC_SUBST(enable_profile)

AC_OUTPUT(config/config.mk)

